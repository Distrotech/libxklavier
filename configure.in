AC_INIT(libxklavier/xklavier.c)

PACKAGE=libxklavier
MAJOR_VERSION=0
MINOR_VERSION=95
VERSION=$MAJOR_VERSION.$MINOR_VERSION
VERSION_INFO=7:0:0

AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(VERSION_INFO)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AM_MAINTAINER_MODE

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
dnl AC_ARG_PROGRAM
AM_PROG_LIBTOOL


dnl From Bruno Haible.
dnl From gnoome-vfs

AC_DEFUN([jm_LANGINFO_CODESET],
[
  AC_CHECK_HEADERS(langinfo.h)
  AC_CHECK_FUNCS(nl_langinfo)

  AC_CACHE_CHECK([for nl_langinfo and CODESET], jm_cv_langinfo_codeset,
    [AC_TRY_LINK([#include <langinfo.h>],
      [char* cs = nl_langinfo(CODESET);],
      jm_cv_langinfo_codeset=yes,
      jm_cv_langinfo_codeset=no)
    ])
  if test $jm_cv_langinfo_codeset = yes; then
    AC_DEFINE(HAVE_LANGINFO_CODESET, 1,
      [Define if you have <langinfo.h> and nl_langinfo(CODESET).])
  fi
])

dnl 

jm_LANGINFO_CODESET
AC_CHECK_FUNCS(setlocale)
AC_PATH_X
AC_SUBST(x_libraries)
AC_ARG_WITH( xkb_base,
             [  --with-xkb-base=DIR        XKB base path (by default it is /usr/X11R6/lib/X11/xkb)],
             xkb_base="$withval", 
             xkb_base="/usr/X11R6/lib/X11/xkb" )
			 
if ! test -d $xkb_base; then
        AC_MSG_ERROR([The path $xkb_base does not denote the directory])
fi

AC_DEFINE_UNQUOTED(XKB_BASE,"${xkb_base}",Base for XKB configuration)

AC_ARG_ENABLE(doxygen, 
[  --disable-doxygen       Do not build doxygen documentation],
, enable_doxygen=yes)

AC_SUBST(DO_DOXYGEN,"${enable_doxygen}")

dnl Checks for libraries.
PKG_CHECK_MODULES(XML, \
	libxml-2.0 >= 2.0.0)
AC_SUBST(XML_LIBS)
AC_SUBST(XML_CFLAGS)

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

AC_OUTPUT([
Makefile
libxklavier/Makefile
libxklavier.spec
Doxyfile
doc/Makefile
doc/html/Makefile
libxklavier.pc
])

