AC_INIT(libxklavier/xklavier.c)

PACKAGE=libxklavier
MAJOR_VERSION=1
MINOR_VERSION=02
VERSION=$MAJOR_VERSION.$MINOR_VERSION
VERSION_INFO=8:0:0

AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(VERSION_INFO)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AM_MAINTAINER_MODE

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
dnl AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AM_ICONV


dnl From Bruno Haible.
dnl From gnoome-vfs

AC_DEFUN([jm_LANGINFO_CODESET],
[
  AC_CHECK_HEADERS(langinfo.h)
  AC_CHECK_FUNCS(nl_langinfo)

  AC_CACHE_CHECK([for nl_langinfo and CODESET], jm_cv_langinfo_codeset,
    [AC_TRY_LINK([#include <langinfo.h>],
      [char* cs = nl_langinfo(CODESET);],
      jm_cv_langinfo_codeset=yes,
      jm_cv_langinfo_codeset=no)
    ])
  if test $jm_cv_langinfo_codeset = yes; then
    AC_DEFINE(HAVE_LANGINFO_CODESET, 1,
      [Define if you have <langinfo.h> and nl_langinfo(CODESET).])
  fi
])

dnl 

jm_LANGINFO_CODESET
AC_CHECK_FUNCS(setlocale)
AC_PATH_X
AC_SUBST(x_libraries)
AC_SUBST(x_includes)
AC_ARG_WITH( xkb_base,
             [  --with-xkb-base=DIR        XKB base path (by default it is /usr/X11R6/lib/X11/xkb)],
             xkb_base="$withval", 
             xkb_base="$x_libraries/X11/xkb" )
			 
if ! test -d $xkb_base; then
        AC_MSG_ERROR([The path $xkb_base does not denote the directory])
fi

AC_DEFINE_UNQUOTED(XKB_BASE,"${xkb_base}",Base for XKB configuration)

ac_xkblib_include="\
#include <stdio.h>
#include <X11/Xlib.h>
#include <X11/XKBlib.h>"

AC_CHECK_HEADER([X11/extensions/XKBrules.h],
                [xkb_headers_present=yes],[],$ac_xkblib_include)

AM_CONDITIONAL(XKB_HEADERS_PRESENT, test "$xkb_headers_present" = "yes")

AC_SUBST(xkbheaders_present)

AC_ARG_ENABLE(doxygen, 
[  --enable-doxygen       Build doxygen documentation],
, enable_doxygen=no)

AC_SUBST(DO_DOXYGEN,"${enable_doxygen}")

dnl Checks for libraries.
PKG_CHECK_MODULES(XML, \
	libxml-2.0 >= 2.0.0)
AC_SUBST(XML_LIBS)
AC_SUBST(XML_CFLAGS)

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

AC_OUTPUT([
Makefile
libxklavier/Makefile
libxklavier.spec
Doxyfile
doc/Makefile
doc/html/Makefile
libxklavier.pc
])

