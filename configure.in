AC_INIT(libxklavier/xklavier.c)

PACKAGE=libxklavier
MAJOR_VERSION=3
MINOR_VERSION=0
VERSION=$MAJOR_VERSION.$MINOR_VERSION
VERSION_INFO=10:0:0

AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(VERSION_INFO)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AM_MAINTAINER_MODE

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
dnl AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AM_ICONV


dnl From Bruno Haible.
dnl From gnoome-vfs

AC_DEFUN([jm_LANGINFO_CODESET],
[
  AC_CHECK_HEADERS(langinfo.h)
  AC_CHECK_FUNCS(nl_langinfo)

  AC_CACHE_CHECK([for nl_langinfo and CODESET], jm_cv_langinfo_codeset,
    [AC_TRY_LINK([#include <langinfo.h>],
      [char* cs = nl_langinfo(CODESET);],
      jm_cv_langinfo_codeset=yes,
      jm_cv_langinfo_codeset=no)
    ])
  if test $jm_cv_langinfo_codeset = yes; then
    AC_DEFINE(HAVE_LANGINFO_CODESET, 1,
      [Define if you have <langinfo.h> and nl_langinfo(CODESET).])
  fi
])

dnl 

jm_LANGINFO_CODESET
AC_CHECK_FUNCS(setlocale)
AC_PATH_X
AC_SUBST(x_libraries)
AC_SUBST(x_includes)
AC_ARG_WITH( xkb_base,
             [  --with-xkb-base=DIR        XKB base path (by default it is /usr/X11R6/lib/X11/xkb)],
             xkb_base="$withval", 
             xkb_base="$x_libraries/X11/xkb" )

AC_DEFINE_UNQUOTED(XKB_BASE,"${xkb_base}",Base for XKB configuration)

AC_ARG_WITH( xkb_bin_base,
             [  --with-xkb-bin-base=DIR        XKB executables base path],
             xkb_bin_base="$withval", 
             xkb_bin_base="$xkb_base" )
			 
AC_DEFINE_UNQUOTED(XKB_BIN_BASE,"${xkb_bin_base}",Base for XKB executables)

AC_ARG_WITH( xkb_default_ruleset,
             [  --with-xkb-default-ruleset=RULES        XKB default set of rules (by default it is base)],
             xkb_default_ruleset="$withval", 
             xkb_default_ruleset="base" )

AC_DEFINE_UNQUOTED(XKB_DEFAULT_RULESET,"${xkb_default_ruleset}",Default XKB set of rules)

AC_ARG_WITH( xmodmap_base,
             [  --with-xmodmap-base=DIR        xmodmap base path (by default it is /usr/share/xmodmap)],
             xmodmap_base="$withval", 
             xmodmap_base="/usr/share/xmodmap" )
			 
AC_DEFINE_UNQUOTED(XMODMAP_BASE,"${xmodmap_base}",Base for xmodmap configuration)

save_CFLAGS="$CFLAGS"
if test -n "$ac_x_includes"; then
  X_INCLUDES=-I`echo $x_includes | sed -e "s/:/ -I/g"`
  CFLAGS="${CFLAGS} $X_INCLUDES"
fi

ac_xkblib_include="\
#include <stdio.h>
#include <X11/Xlib.h>
#include <X11/XKBlib.h>"

AC_CHECK_HEADER([X11/extensions/XKBrules.h],
                [xkb_headers_present=yes],[],$ac_xkblib_include)

AM_CONDITIONAL(XKB_HEADERS_PRESENT, test "$xkb_headers_present" = "yes")

AC_SUBST(xkbheaders_present)

CFLAGS="$save_CFLAGS"

AC_ARG_ENABLE(doxygen, 
[  --enable-doxygen       Build doxygen documentation],
, enable_doxygen=no)

AC_SUBST(DO_DOXYGEN,"${enable_doxygen}")

AC_ARG_ENABLE(xkb-support, 
[  --enable-xkb-support       Enable XKB support],
, enable_xkb_support=yes)

AM_CONDITIONAL(ENABLE_XKB_SUPPORT, test "$enable_xkb_support" = "yes")

AC_ARG_ENABLE(xmm-support, 
[  --enable-xmm-support       Enable xmodmap support],
, enable_xmm_support=yes)

AM_CONDITIONAL(ENABLE_XMM_SUPPORT, test "$enable_xmm_support" = "yes")

AC_MSG_CHECKING(whether at least one libxklavier backend is enabled)
if test \( "$enable_xkb_support" == "yes" -a \
          "$xkb_headers_present" == "yes" \) -o \
        "$enable_xmm_support" == "yes" ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([no libxklavier backends configured])
fi

dnl Checks for libraries.
PKG_CHECK_MODULES(XML, \
	libxml-2.0 >= 2.0.0)
AC_SUBST(XML_LIBS)
AC_SUBST(XML_CFLAGS)

PKG_CHECK_MODULES(GLIB, \
	glib-2.0 >= 2.6.0, gobject-2.0 >= 2.6.0)
AC_SUBST(GLIB_LIBS)
AC_SUBST(GLIB_CFLAGS)

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

AC_OUTPUT([
Makefile
libxklavier/Makefile
libxklavier.spec
Doxyfile
doc/Makefile
doc/html/Makefile
tests/Makefile
libxklavier.pc
])

echo '**********************************************************'
echo '  Libxklavier is configured with the following backends:'
if test "$enable_xkb_support" == "yes" ; then
  if test "$xkb_headers_present" == "yes" ; then
    echo '    XKB (libxkbfile headers are present)'
  else
    echo '    XKB (but libxkbfile headers are NOT present)'
  fi
fi
if test "$enable_xmm_support" == "yes" ; then
  echo '    xmodmap'
fi
echo '**********************************************************'
